package Test

type Statechart {
    feature region : Region
    feature abstractTransitions : AbstractTransition[0..*]

    feature transitions :> abstractTransitions : Transition[0..*]
    feature entryTransitions :> abstractTransitions : EntryTransition[0..*]

    init {
        inline seq entryTransitions -> main
    }

    tran {
        inline choice transitions -> main else { }
    }
}

type Region {
    feature abstractStates : AbstractState[0..*]
    feature states :> abstractStates : State[0..*]
    feature entryStates :> abstractStates : EntryState[0..*]

    var activeState : states[0..1]
}

type AbstractState {
    reference region : Region

    tran exit() {
        region.activeState := Nothing
    }
    tran enter() {
        region.activeState := Self
    }
}

type State : AbstractState { }
type EntryState : AbstractState { }

type AbstractTransition {
    reference from : AbstractState
    reference to : AbstractState

    virtual tran {
        assume (true)
    }
}

type Transition : AbstractTransition {
    override tran {
        inline from.exit()
        inline to.enter()
    }
}

type EntryTransition : Transition {
    override tran {
        inline to.enter()
    }
}

type SimpleStatechart : Statechart {
    instance Main :> region : Region {
        instance Entry :> entryStates : EntryState {
            reference region <- Main
        }
        instance Idle :> states : State {
            reference region <- Main
        }
        instance Operation :> states : State {
            reference region <- Main
        }
    }

    instance t1 :> entryTransitions : EntryTransition {
        reference from <- Main.Entry
        reference to <- Main.Idle
    }
    instance t2 :> transitions : Transition {
        reference from <- Main.Idle
        reference to <- Main.Operation
    }
}

target Mission {
    instance statechart : SimpleStatechart

    init {
        inline statechart.init()
    }

    tran {
        inline statechart.main()
    }

    prop {
        statechart.Main.activeState != statechart.Main.Idle
    }
}
