package Test

// Event.oxsts

type Event {
    var isActive: Boolean := false

    tran set {
        isActive := true
    }

    tran reset {
        isActive := false
    }

    havoc {
        havoc (isActive)
    }

    tran isSet {
        assume (isActive)
    }
}

type Timeout {
    var deltaTime : Integer := 0

    tran {
        if (deltaTime > 0) {
            deltaTime := deltaTime - 1
        }
    }

    tran isUp {
        assume (deltaTime <= 0)
    }
}

// Trigger.oxsts

type Trigger {
    virtual tran isTriggered {
        assume (true)
    }
}

type EventTrigger : Trigger {
    reference event : Event[1..1]

    override tran isTriggered {
        inline event.isSet()
    }
}

type TimeoutTrigger : Trigger {
    reference timeout : Timeout[1..1]

    override tran isTriggered {
        inline timeout.isUp()
    }
}

// Action.oxsts

type Action {
    virtual tran { }
}

type RaiseEventAction : Action {
    reference event : Event[1..1]

    override tran {
        inline event.set()
    }
}

type SetTimeoutAction : Action {
    reference timeout : Timeout[1..1]
}

// Component.oxsts

type Component {
    virtual init { }
    virtual tran { }
}

type Channel {
    reference inputEvent : Event
    reference outputEvent : Event

    tran {
        if (inputEvent.isActive) {
            inline outputEvent.set()
        }
    }
}

type CompositeComponent : Component {
    feature components : Component
    feature channels : Channel
}

type SyncComponent : CompositeComponent {
    override init {
        inline seq components -> init
    }

    override tran {
        inline seq components -> main
        inline seq channels -> main
    }
}

// Statechart.oxsts

type Statechart : Component {
    feature events : Event[0..*]
    feature inputEvents :> events : Event[0..*]
    feature outputEvents :> events : Event[0..*]

    feature timeouts : Timeout[0..*]

    feature regions : Region[0..*]

    override init {
        inline seq regions -> activateRecursive
    }

    override tran {
        inline seq outputEvents -> reset
        inline choice regions -> fireTransitions else { }
        inline seq inputEvents -> reset
        inline seq timeouts -> main
    }
}

type Region {
    feature states : State[0..*]

    var activeState : states[0..1] := Nothing

    feature abstractTransitions : AbstractTransition[0..*]

    feature transitions :> abstractTransitions : Transition[0..*]
    feature entryTransitions :> abstractTransitions : EntryTransition[0..*]

    tran activateRecursive {
        inline seq entryTransitions -> main
    }

    tran deactivateRecursive {
        inline seq states -> deactivateRecursive
    }

    tran fireTransitions {
        inline choice transitions -> main else {
            inline choice states -> fireTransitions
        }
    }

    tran fireTransitionsInner {
        inline choice transitions -> main
    }
}

type State {
    reference parent : Region[0..1]
    reference parentState : State[0..1]
    feature regions : Region[0..1]

    feature entryActions : Action[0..*]
    feature exitActions : Action[0..*]

    tran isActive {
        assume (parent.activeState = Self)
    }

    tran deactivateRecursive {
        inline seq regions -> deactivateRecursive

        if (parent.activeState = Self) {
            parent.activeState := Nothing
            inline seq exitActions -> main
        }
    }

    tran exitRecursive(commonRegion : Region) {
        inline seq exitActions -> main

        parent.activeState := Nothing

        inline if (commonRegion != parent) {
            inline parentState.exitRecursive(commonRegion)
        }
    }

    tran exit(commonRegion : Region) {
        inline exitRecursive(commonRegion)

        inline seq regions -> deactivateRecursive
    }

    tran enterRecursive(commonRegion : Region) {
        inline if (commonRegion != parent) {
            inline parentState.enterRecursive(commonRegion)
        }

        parent.activeState := Self

        inline seq entryActions -> main
    }

    tran enter(commonRegion : Region) {
        inline enterRecursive(commonRegion)

        inline seq regions -> activateRecursive
    }

    tran fireTransitions {
        inline seq regions -> fireTransitionsInner
    }
}

type AbstractTransition {
    reference commonRegion : Region[1..1]
    reference to : State[1..1]

    virtual tran {
        assume (true)
    }
}

type EntryTransition : AbstractTransition {
    override tran {
        inline to.enter(commonRegion)
    }
}

type Transition : AbstractTransition {
    reference from : State[0..1]

    feature trigger : Trigger
    feature actions : Action[0..*]

    tran {
        inline trigger.isTriggered()
        inline from.isActive()

        inline from.exit(commonRegion)
        inline seq actions -> main
        inline to.enter(commonRegion)
    }
}

// Controller.oxsts

type SetTimeoutActionOneS : SetTimeoutAction {
    override tran {
        timeout.deltaTime := 1
    }
}

type SetTimeoutActionTwoS : SetTimeoutAction {
    override tran {
        timeout.deltaTime := 2
    }
}

type Controller : Statechart {
    instance policeEvent :> inputEvents : Event

    instance policeEventA :> outputEvents : Event
    instance toggleEventA :> outputEvents : Event

    instance policeEventB :> outputEvents : Event
    instance toggleEventB :> outputEvents : Event

    instance trafficTimeout :> timeouts : Timeout
    instance stopTimeout :> timeouts : Timeout

    instance Main :> regions : Region {
        instance et1 :> entryTransitions : EntryTransition {
            reference commonRegion <- Main
            reference to <- Operating
        }

        instance Operating :> states : State {
            reference parent <- Main

            instance OperatingRegion :> regions : Region {
                instance et2 :> entryTransitions : EntryTransition {
                    reference commonRegion <- OperatingRegion
                    reference to <- Init
                }

                instance Init :> states : State {
                    reference parent <- OperatingRegion
                    reference parentState <- Operating

                    instance ea1 :> entryActions : SetTimeoutActionOneS {
                        reference timeout <- stopTimeout
                    }
                }

                instance initToTrafficOnA :> transitions : Transition {
                    reference commonRegion <- Main
                    reference from <- Init
                    reference to <- TrafficOnA

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- stopTimeout
                    }
                    instance a :> actions : RaiseEventAction {
                        reference event <- toggleEventA // switch A to Green
                    }
                }

                instance TrafficOnA :> states : State {
                    reference parent <- OperatingRegion
                    reference parentState <- Operating

                    instance a :> entryActions : SetTimeoutActionTwoS {
                        reference timeout <- trafficTimeout
                    }
                }

                instance trafficOnAToStoppingA :> transitions : Transition {
                    reference commonRegion <- Main
                    reference from <- TrafficOnA
                    reference to <- StoppingA

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- trafficTimeout
                    }
                    instance a :> actions : RaiseEventAction {
                        reference event <- toggleEventA // switch A to Yellow
                    }
                }

                instance StoppingA :> states : State {
                    reference parent <- OperatingRegion
                    reference parentState <- Operating

                    instance a :> entryActions : SetTimeoutActionTwoS {
                        reference timeout <- stopTimeout
                    }
                }

                instance stoppingAToTrafficOnB :> transitions : Transition {
                    reference commonRegion <- Main
                    reference from <- StoppingA
                    reference to <- TrafficOnB

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- stopTimeout
                    }
                    instance a1 :> actions : RaiseEventAction {
                        reference event <- toggleEventA // switch A to Red
                    }
                    instance a2 :> actions : RaiseEventAction {
                        reference event <- toggleEventB // switch B to Green
                    }
                }

                instance TrafficOnB :> states : State {
                    reference parent <- OperatingRegion
                    reference parentState <- Operating

                    instance a :> entryActions : SetTimeoutActionOneS {
                        reference timeout <- trafficTimeout
                    }
                }

                instance trafficOnBToStoppingB :> transitions : Transition {
                    reference commonRegion <- Main
                    reference from <- TrafficOnB
                    reference to <- StoppingB

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- trafficTimeout
                    }
                    instance a :> actions : RaiseEventAction {
                        reference event <- toggleEventB // switch B to Yellow
                    }
                }

                instance StoppingB :> states : State {
                    reference parent <- OperatingRegion
                    reference parentState <- Operating

                    instance a :> entryActions : SetTimeoutActionOneS {
                        reference timeout <- stopTimeout
                    }
                }

                instance stoppingBToTrafficOnA :> transitions : Transition {
                    reference commonRegion <- Main
                    reference from <- StoppingB
                    reference to <- TrafficOnA

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- stopTimeout
                    }
                    instance a1 :> actions : RaiseEventAction {
                        reference event <- toggleEventB // switch B to Red
                    }
                    instance a2 :> actions : RaiseEventAction {
                        reference event <- toggleEventA // switch A to Green
                    }
                }
            }
        }

        instance operatingToInterrupted :> transitions : Transition {
            reference commonRegion <- Main
            reference from <- Operating
            reference to <- Interrupted

            instance t :> trigger : EventTrigger {
                reference event <- policeEvent
            }
        }

        instance interruptedToOperating :> transitions : Transition {
            reference commonRegion <- Main
            reference from <- Interrupted
            reference to <- Operating

            instance t :> trigger : EventTrigger {
                reference event <- policeEvent
            }
        }

        instance Interrupted :> states : State {
            reference parent <- Main

            instance ea1 :> entryActions : RaiseEventAction {
                reference event <- policeEventA
            }
            instance ea2 :> entryActions : RaiseEventAction {
                reference event <- policeEventB
            }
            instance ea3 :> exitActions : RaiseEventAction {
                reference event <- policeEventA
            }
            instance ea4 :> exitActions : RaiseEventAction {
                reference event <- policeEventB
            }
        }
    }
}

// TrafficLightCtrl.oxsts

type TrafficLightCtrl : Statechart {
    instance policeEvent :> inputEvents : Event
    instance toggleEvent :> inputEvents : Event

    instance displayRedEvent :> outputEvents : Event
    instance displayYellowEvent :> outputEvents : Event
    instance displayGreenEvent :> outputEvents : Event
    instance displayNoneEvent :> outputEvents : Event

    instance blinkingTimeout :> timeouts : Timeout

    instance Main :> regions : Region {
        instance et :> entryTransitions : EntryTransition {
            reference commonRegion <- Main
            reference to <- Normal
        }

        instance Normal :> states : State {
            reference parent <- Main

            instance NormalRegion :> regions : Region {
                instance et2 :> entryTransitions : EntryTransition {
                    reference commonRegion <- NormalRegion
                    reference to <- Red
                }

                instance Red :> states : State {
                    reference parent <- NormalRegion
                    reference parentState <- Normal

                    instance e :> entryActions : RaiseEventAction {
                        reference event <- displayRedEvent
                    }
                }

                instance redToGreen :> transitions : Transition {
                    reference commonRegion <- NormalRegion
                    reference from <- Red
                    reference to <- Green

                    instance t :> trigger : EventTrigger {
                        reference event <- toggleEvent
                    }
                }

                instance Green :> states : State {
                    reference parent <- NormalRegion
                    reference parentState <- Normal

                    instance e :> entryActions : RaiseEventAction {
                        reference event <- displayGreenEvent
                    }
                }

                instance greenToYellow :> transitions : Transition {
                    reference commonRegion <- NormalRegion
                    reference from <- Green
                    reference to <- Yellow

                    instance t :> trigger : EventTrigger {
                        reference event <- toggleEvent
                    }
                }

                instance Yellow :> states : State {
                    reference parent <- NormalRegion
                    reference parentState <- Normal

                    instance e :> entryActions : RaiseEventAction {
                        reference event <- displayYellowEvent
                    }
                }

                instance yellowToRed :> transitions : Transition {
                    reference commonRegion <- NormalRegion
                    reference from <- Yellow
                    reference to <- Red

                    instance t :> trigger : EventTrigger {
                        reference event <- toggleEvent
                    }
                }
            }
        }

        instance normalToInterrupt :> transitions : Transition {
            reference commonRegion <- Main
            reference from <- Normal
            reference to <- Interrupted

            instance t :> trigger : EventTrigger {
                reference event <- policeEvent
            }
        }

        instance interruptToNormal :> transitions : Transition {
            reference commonRegion <- Main
            reference from <- Interrupted
            reference to <- Normal

            instance t :> trigger : EventTrigger {
                reference event <- policeEvent
            }
        }

        instance Interrupted :> states : State {
            reference parent <- Main

            instance InterruptedRegion :> regions : Region {
                instance et :> entryTransitions : EntryTransition {
                    reference commonRegion <- InterruptedRegion
                    reference to <- Black
                }

                instance Black :> states : State {
                    reference parent <- InterruptedRegion
                    reference parentState <- Interrupted

                    instance ea1 :> entryActions : SetTimeoutActionOneS {
                        reference timeout <- blinkingTimeout
                    }
                    instance ea2 :> entryActions : RaiseEventAction {
                        reference event <- displayNoneEvent
                    }
                }

                instance blackToYellow :> transitions : Transition {
                    reference commonRegion <- InterruptedRegion
                    reference from <- Black
                    reference to <- Yellow

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- blinkingTimeout
                    }
                }

                instance yellowToBlack :> transitions : Transition {
                    reference commonRegion <- InterruptedRegion
                    reference from <- Yellow
                    reference to <- Black

                    instance t :> trigger : TimeoutTrigger {
                        reference timeout <- blinkingTimeout
                    }
                }

                instance Yellow :> states : State {
                    reference parent <- InterruptedRegion
                    reference parentState <- Interrupted

                    instance ea1 :> entryActions : SetTimeoutActionOneS {
                        reference timeout <- blinkingTimeout
                    }
                    instance ea2 :> entryActions : RaiseEventAction {
                        reference event <- displayYellowEvent
                    }
                }

            }
        }
    }
}

type Crossroad : SyncComponent {
    instance controller :> components : Controller
    instance trafficLightA :> components : TrafficLightCtrl
    instance trafficLightB :> components : TrafficLightCtrl

    instance policeAChannel :> channels : Channel {
        reference inputEvent <- controller.policeEventA
        reference outputEvent <- trafficLightA.policeEvent
    }
    instance toggleAChannel :> channels : Channel {
        reference inputEvent <- controller.toggleEventA
        reference outputEvent <- trafficLightA.toggleEvent
    }

    instance policeBChannel :> channels : Channel {
        reference inputEvent <- controller.policeEventB
        reference outputEvent <- trafficLightB.policeEvent
    }
    instance toggleBChannel :> channels : Channel {
        reference inputEvent <- controller.toggleEventB
        reference outputEvent <- trafficLightB.toggleEvent
    }
}

abstract target CrossroadsMission {
    instance crossroad : Crossroad

    init {
        inline crossroad.init()
    }

    tran {
        inline crossroad.controller.policeEvent.havoc
        inline crossroad.main()
    }
}

// Controller state tests

target ControllerOperating_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.activeState = crossroad.controller.Main.Operating)
    }
}

target ControllerOperatingInit_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.Operating.OperatingRegion.activeState = crossroad.controller.Main.Operating.OperatingRegion.Init)
    }
}

target ControllerOperatingTrafficOnA_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.Operating.OperatingRegion.activeState = crossroad.controller.Main.Operating.OperatingRegion.TrafficOnA)
    }
}

target ControllerOperatingStoppingA_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.Operating.OperatingRegion.activeState = crossroad.controller.Main.Operating.OperatingRegion.StoppingA)
    }
}

target ControllerOperatingTrafficOnB_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.Operating.OperatingRegion.activeState = crossroad.controller.Main.Operating.OperatingRegion.TrafficOnB)
    }
}

target ControllerOperatingStoppingB_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.Operating.OperatingRegion.activeState = crossroad.controller.Main.Operating.OperatingRegion.StoppingB)
    }
}

target ControllerInterrupted_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.controller.Main.activeState = crossroad.controller.Main.Interrupted)
    }
}

// TrafficLightA state tests

target TrafficLightANormal_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Normal)
    }
}

target TrafficLightANormalRed_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Red)
    }
}

target TrafficLightANormalGreen_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Green)
    }
}

target TrafficLightANormalYellow_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Yellow)
    }
}

target TrafficLightAInterrupted_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Interrupted)
    }
}

target TrafficLightAInterruptedBlack_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.activeState = crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.Black)
    }
}

target TrafficLightAInterruptedYellow_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.activeState = crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.Yellow)
    }
}

// TrafficLightB state tests

target TrafficLightBNormal_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Normal)
    }
}

target TrafficLightBNormalRed_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Red)
    }
}

target TrafficLightBNormalGreen_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Green)
    }
}

target TrafficLightBNormalYellow_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Yellow)
    }
}

target TrafficLightBInterrupted_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Interrupted)
    }
}

target TrafficLightBInterruptedBlack_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.activeState = crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.Black)
    }
}

target TrafficLightBInterruptedYellow_Unsafe : CrossroadsMission {
    prop {
        ! (crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.activeState = crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.Yellow)
    }
}

// Incorrect state tests

target ControllerOperatingInterruptedExclusive_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.controller.Main.activeState = crossroad.controller.Main.Interrupted &&
            crossroad.controller.Main.Operating.OperatingRegion.activeState != Nothing
        )
    }
}

target ControllerOperatingIncorrectStateHierarchy_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.controller.Main.activeState = crossroad.controller.Main.Operating &&
            crossroad.controller.Main.Operating.OperatingRegion.activeState != crossroad.controller.Main.Operating.OperatingRegion.Init &&
            crossroad.controller.Main.Operating.OperatingRegion.activeState != crossroad.controller.Main.Operating.OperatingRegion.TrafficOnA &&
            crossroad.controller.Main.Operating.OperatingRegion.activeState != crossroad.controller.Main.Operating.OperatingRegion.StoppingA &&
            crossroad.controller.Main.Operating.OperatingRegion.activeState != crossroad.controller.Main.Operating.OperatingRegion.TrafficOnB &&
            crossroad.controller.Main.Operating.OperatingRegion.activeState != crossroad.controller.Main.Operating.OperatingRegion.StoppingB
        )
    }
}

target TrafficLightANormalInterruptedExclusive_Safe : CrossroadsMission {
    prop {
        ! (
            (
                crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Normal &&
                crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.activeState != Nothing
            ) || (
                crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Interrupted &&
                crossroad.trafficLightA.Main.Normal.NormalRegion.activeState != Nothing
            )
        )
    }
}

target TrafficLightANormalIncorrectStateHierarchy_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Normal &&
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState != crossroad.trafficLightA.Main.Normal.NormalRegion.Red &&
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState != crossroad.trafficLightA.Main.Normal.NormalRegion.Green &&
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState != crossroad.trafficLightA.Main.Normal.NormalRegion.Yellow
        )
    }
}

target TrafficLightAInterruptedIncorrectStateHierarchy_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Interrupted &&
            crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.activeState != crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.Black &&
            crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.activeState != crossroad.trafficLightA.Main.Interrupted.InterruptedRegion.Yellow
        )
    }
}

target TrafficLightBNormalInterruptedExclusive_Safe : CrossroadsMission {
    prop {
        ! (
            (
                crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Normal &&
                crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.activeState != Nothing
            ) || (
                crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Interrupted &&
                crossroad.trafficLightB.Main.Normal.NormalRegion.activeState != Nothing
            )
        )
    }
}

target TrafficLightBNormalIncorrectStateHierarchy_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Normal &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState != crossroad.trafficLightB.Main.Normal.NormalRegion.Red &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState != crossroad.trafficLightB.Main.Normal.NormalRegion.Green &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState != crossroad.trafficLightB.Main.Normal.NormalRegion.Yellow
        )
    }
}

target TrafficLightBInterruptedIncorrectStateHierarchy_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Interrupted &&
            crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.activeState != crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.Black &&
            crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.activeState != crossroad.trafficLightB.Main.Interrupted.InterruptedRegion.Yellow
        )
    }
}

// Behaviour tests

target BothMustBeInterruptedAtTheSameTime_Unsafe : CrossroadsMission {
    prop {
        ! (
            (
                crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Interrupted &&
                crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Interrupted
            )
        )
    }
}

target BothMustBeInterruptedAtTheSameTime_Safe : CrossroadsMission {
    prop {
        ! (
            (
                crossroad.trafficLightA.Main.activeState = crossroad.trafficLightA.Main.Interrupted &&
                crossroad.trafficLightB.Main.activeState != crossroad.trafficLightB.Main.Interrupted
            ) || (
                crossroad.trafficLightA.Main.activeState != crossroad.trafficLightA.Main.Interrupted &&
                crossroad.trafficLightB.Main.activeState = crossroad.trafficLightB.Main.Interrupted
            )
        )
    }
}

target BothCannotBeGreen_Safe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Green &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Green
        )
    }
}

target ARedBRed_Unsafe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Red &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Red
        )
    }
}

target AGreenBRed_Unsafe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Green &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Red
        )
    }
}

target AYellowBRed_Unsafe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Yellow &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Red
        )
    }
}

target ARedBGreen_Unsafe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Red &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Green
        )
    }
}

target ARedBYellow_Unsafe : CrossroadsMission {
    prop {
        ! (
            crossroad.trafficLightA.Main.Normal.NormalRegion.activeState = crossroad.trafficLightA.Main.Normal.NormalRegion.Red &&
            crossroad.trafficLightB.Main.Normal.NormalRegion.activeState = crossroad.trafficLightB.Main.Normal.NormalRegion.Yellow
        )
    }
}
