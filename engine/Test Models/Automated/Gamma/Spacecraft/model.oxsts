/*
 * SPDX-FileCopyrightText: 2023-2024 The Semantifyr Authors
 *
 * SPDX-License-Identifier: EPL-2.0
 */

package Test

import Statecharts
import Components
import Triggers
import Actions
import Events

type Station : Statechart {
    containment dataEvent :> inputEvents : Event
    containment startEvent :> inputEvents : Event
    containment shutdownEvent :> inputEvents : Event

    containment pingEvent :> outputEvents : Event

    containment pingTimeout :> timeouts : Timeout
    containment startTimeout :> timeouts : Timeout

    var receivedData : Integer = 0

    containment Main :> regions : Region {
        containment et1 :> entryTransitions : EntryTransition {
            reference ::> commonRegion : Region = Main
            reference ::> to : State = Idle
        }

        containment Idle :> states : State {
            containment ea1 :> entryActions : SetTimeoutAction {
                reference ::> timeout : Timeout = startTimeout
                reference ::> amount : Integer = 1500
            }
        }

        containment startOperation :> transitions : Transition {
            reference ::> commonRegion : Region = Main
            reference ::> from : State = Idle
            reference ::> to : State = Operation

            containment t :> trigger : EventTrigger {
                reference ::> event : Event = startEvent
            }
        }

        containment autoStartOperation :> transitions : Transition {
            reference ::> commonRegion : Region = Main
            reference ::> from : State = Idle
            reference ::> to : State = Operation

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = startTimeout
            }
        }

        containment shutdownOperation :> transitions : Transition {
            reference ::> commonRegion : Region = Main
            reference ::> from : State = Operation
            reference ::> to : State = Idle

            containment t :> trigger : EventTrigger {
                reference ::> event : Event = shutdownEvent
            }
        }

        containment Operation :> states : State {
            containment ea1 :> entryActions : SetTimeoutAction {
                reference ::> timeout : Timeout = pingTimeout
                reference ::> amount : Integer = 5000
            }
            containment ea2 :> entryActions : RaiseEventAction {
                reference ::> event : Event = pingEvent
            }

            containment ReceiveData :> regions : Region {
                containment et2 :> entryTransitions : EntryTransition {
                    reference ::> commonRegion : Region = ReceiveData
                    reference ::> to : State = Waiting
                }

                containment Waiting :> states : State

                containment receivedDataOperation :> transitions : Transition {
                    reference ::> commonRegion : Region = ReceiveData
                    reference ::> from : State = Waiting
                    reference ::> to : State = Waiting

                    containment t :> trigger : EventTrigger {
                        reference ::> event : Event = dataEvent
                    }
                    containment a1 :> actions : Action {
                        tran {
                            receivedData := receivedData + 1
                        }
                    }
                    containment a2 :> actions : RaiseEventAction {
                        reference ::> event : Event = pingEvent
                    }
                }
            }
        }

        containment resendPing :> transitions : Transition {
            reference ::> commonRegion : Region = Main
            reference ::> from : State = Operation
            reference ::> to : State = Operation

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = pingTimeout
            }
        }
    }
}

// TrafficLightCtrl.oxsts

type Spacecraft : Statechart {
    containment pingEvent :> inputEvents : Event
    containment dataEvent :> outputEvents : Event

    containment rechargeTimeout :> timeouts : Timeout
    containment consumeTimeout :> timeouts : Timeout
    containment transmitTimeout :> timeouts : Timeout

    var batteryCharge : Integer = 100
    var recharging : Boolean = false
    var data : Integer = 10

    containment Communication :> regions : Region {
        containment et1 :> entryTransitions : EntryTransition {
            reference ::> commonRegion : Region = Communication
            reference ::> to : State = WaitingPing
        }

        containment WaitingPing :> states : State

        containment receivedPing :> transitions : Transition {
            reference ::> commonRegion : Region = Communication
            reference ::> from : State = WaitingPing
            reference ::> to : State = Transmitting

            containment t :> trigger : EventTrigger {
                reference ::> event : Event = pingEvent
            }
            containment g :> guards : Guard {
                override tran {
                    assume (recharging == false)
                }
            }
        }

        containment lowBattery :> transitions : Transition {
            reference ::> commonRegion : Region = Communication
            reference ::> from : State = Transmitting
            reference ::> to : State = WaitingPing

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = consumeTimeout
            }
            containment g :> guards : Guard {
                override tran {
                    assume (batteryCharge < 40)
                }
            }
        }

        containment lowBatteryOrDone :> transitions : Transition {
            reference ::> commonRegion : Region = Communication
            reference ::> from : State = Transmitting
            reference ::> to : State = WaitingPing

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = transmitTimeout
            }
            containment g :> guards : Guard {
                override tran {
                    assume (data <= 0 || batteryCharge < 40)
                }
            }
        }

        containment Transmitting :> states : State {
            containment SendData :> regions : Region {
                containment et2 :> entryTransitions : EntryTransition {
                    reference ::> commonRegion : Region = SendData
                    reference ::> to : State = Sending
                }

                containment Sending :> states : State {
                    containment ea1 :> entryActions : SetTimeoutAction {
                        reference ::> timeout : Timeout = transmitTimeout
                        reference ::> amount : Integer = 1500
                    }
                }

                containment transmitData :> transitions : Transition {
                    reference ::> commonRegion : Region = SendData
                    reference ::> from : State = Sending
                    reference ::> to : State = Sending

                    containment t :> trigger : TimeoutTrigger {
                        reference ::> timeout : Timeout = transmitTimeout
                    }
                    containment g :> guards : Guard {
                        override tran {
                            assume (! (data <= 0 || batteryCharge < 40))
                        }
                    }
                    containment a1 :> actions : Action {
                        override tran {
                            data := data - 1
                        }
                    }
                    containment a2 :> actions : RaiseEventAction {
                        reference ::> event : Event = dataEvent
                    }
                }
            }

            containment ConsumePower :> regions : Region {
                containment et2 :> entryTransitions : EntryTransition {
                    reference ::> commonRegion : Region = ConsumePower
                    reference ::> to : State = Consuming
                }

                containment Consuming :> states : State {
                    containment ea1 :> entryActions : SetTimeoutAction {
                        reference ::> timeout : Timeout = consumeTimeout
                        reference ::> amount : Integer = 1000
                    }
                }

                containment consumePower :> transitions : Transition {
                    reference ::> commonRegion : Region = ConsumePower
                    reference ::> from : State = Consuming
                    reference ::> to : State = Consuming

                    containment t :> trigger : TimeoutTrigger {
                        reference ::> timeout : Timeout = consumeTimeout
                    }
                    containment g :> guards : Guard {
                        override tran {
                            assume (batteryCharge >= 40)
                        }
                    }
                    containment a :> actions : Action {
                        override tran {
                            batteryCharge := batteryCharge - 10
                        }
                    }
                }
            }
        }
    }

    containment Battery :> regions : Region {
        containment et1 :> entryTransitions : EntryTransition {
            reference ::> commonRegion : Region = Battery
            reference ::> to : State = NotRecharging
        }

        containment NotRecharging :> states : State {
            containment ea1 :> entryActions : Action {
                tran {
                    recharging := false
                }
            }
        }

        containment startRecharging :> transitions : Transition {
            reference ::> commonRegion : Region = Battery
            reference ::> from : State = NotRecharging
            reference ::> to : State = Recharging

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = consumeTimeout
            }
            containment g :> guards : Guard {
                override tran {
                    assume (batteryCharge < 80)
                }
            }
        }

        containment recharge :> transitions : Transition {
            reference ::> commonRegion : Region = Battery
            reference ::> from : State = Recharging
            reference ::> to : State = Recharging

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = rechargeTimeout
            }
            containment g :> guards : Guard {
                override tran {
                    assume (batteryCharge < 100)
                }
            }
            containment a :> actions : Action {
                override tran {
                    batteryCharge := batteryCharge + 10
                }
            }
        }

        containment stopRecharging :> transitions : Transition {
            reference ::> commonRegion : Region = Battery
            reference ::> from : State = Recharging
            reference ::> to : State = NotRecharging

            containment t :> trigger : TimeoutTrigger {
                reference ::> timeout : Timeout = rechargeTimeout
            }
            containment g :> guards : Guard {
                override tran {
                    assume (batteryCharge == 100)
                }
            }
        }

        containment Recharging :> states : State {
            containment ea1 :> entryActions : SetTimeoutAction {
                reference ::> timeout : Timeout = rechargeTimeout
                reference ::> amount : Integer = 2000
            }
            containment ea2 :> entryActions : Action {
                tran {
                    recharging := true
                }
            }
        }
    }
}

type SpaceMission : SyncComponent {
    containment station :> components : Station
    containment spacecraft :> components : Spacecraft

    //containment startEvent :> inputEvents : Event
    //containment shutdownEvent :> inputEvents : Event

    containment pingChannel :> channels : Channel {
        reference ::> inputEvent : Event = station.pingEvent
        reference ::> outputEvent : Event = spacecraft.pingEvent
    }
    containment dataChannel :> channels : Channel {
        reference ::> inputEvent : Event = spacecraft.dataEvent
        reference ::> outputEvent : Event = station.dataEvent
    }
    /*containment startChannel :> channels : Channel {
        reference ::> inputEvent : Event = startEvent
        reference ::> outputEvent : Event = station.startEvent
    }
    containment shutdownChannel :> channels : Channel {
        reference ::> inputEvent : Event = shutdownEvent
        reference ::> outputEvent : Event = station.shutdownEvent
    }*/
}

target Mission {
    containment spaceMission : SpaceMission

    init {
        inline spaceMission.init()
    }

    tran {
        //inline spaceMission.startEvent.havoc()
        //inline spaceMission.shutdownEvent.havoc()

        inline spaceMission.station.startEvent.havoc()
        inline spaceMission.station.shutdownEvent.havoc()

        inline spaceMission.main()
        inline spaceMission.passTime()
    }

    prop {
        true
    }
}

target Ping_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.pingEvent.isActive)
    }
}

target StationIdle_Unsafe : Mission {
    prop {
       ! (spaceMission.station.Main.activeState == spaceMission.station.Main.Idle)
    }
}

target StationOperation_Unsafe : Mission {
    prop {
       ! (spaceMission.station.Main.activeState == spaceMission.station.Main.Operation)
    }
}

target StationOperationWaiting_Unsafe : Mission {
    prop {
       ! (spaceMission.station.Main.Operation.ReceiveData.activeState == spaceMission.station.Main.Operation.ReceiveData.Waiting)
    }
}

target SpacecraftWaitingPing_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.Communication.activeState == spaceMission.spacecraft.Communication.WaitingPing)
    }
}

target SpacecraftTransmitting_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.Communication.activeState == spaceMission.spacecraft.Communication.Transmitting)
    }
}

target SpacecraftTransmittingSending_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.Communication.Transmitting.SendData.activeState == spaceMission.spacecraft.Communication.Transmitting.SendData.Sending)
    }
}

target SpacecraftTransmittingConsuming_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.Communication.Transmitting.ConsumePower.activeState == spaceMission.spacecraft.Communication.Transmitting.ConsumePower.Consuming)
    }
}

target SpacecraftNotRecharging_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.Battery.activeState == spaceMission.spacecraft.Battery.NotRecharging)
    }
}

target SpacecraftRecharging_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.Battery.activeState == spaceMission.spacecraft.Battery.Recharging)
    }
}

target Station_ReceivedData_0_Unsafe : Mission {
    prop {
       ! (spaceMission.station.receivedData == 0)
    }
}

/*
target Station_ReceivedData_1_Unsafe : Mission {
    prop {
       ! (spaceMission.station.receivedData == 1)
    }
}

target Station_ReceivedData_5_Unsafe : Mission {
    prop {
       ! (spaceMission.station.receivedData == 5)
    }
}

target Station_ReceivedData_9_Unsafe : Mission {
    prop {
       ! (spaceMission.station.receivedData == 9)
    }
}

target Station_ReceivedData_10_Unsafe : Mission {
    prop {
       ! (spaceMission.station.receivedData == 10)
    }
}
*/

target Spacecraft_data_10_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 10)
    }
}

target Spacecraft_data_9_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 9)
    }
}

/*
target Spacecraft_data_5_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 5)
    }
}

target Spacecraft_data_3_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 3)
    }
}

target Spacecraft_data_2_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 2)
    }
}

target Spacecraft_data_1_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 1)
    }
}

target Spacecraft_data_0_Unsafe : Mission {
    prop {
       ! (spaceMission.spacecraft.data == 0)
    }
}
*/

target Spacecraft_batteryCharge_100_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.batteryCharge == 100)
    }
}

/*
target Spacecraft_batteryCharge_90_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.batteryCharge == 90)
    }
}

target Spacecraft_batteryCharge_80_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.batteryCharge == 80)
    }
}

target Spacecraft_batteryCharge_70_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.batteryCharge == 70)
    }
}

target Spacecraft_batteryCharge_40_Unsafe : Mission {
    prop {
        ! (spaceMission.spacecraft.batteryCharge == 40)
    }
}
*/

target Data_is_lost_if_state_shut_down_Unsafe : Mission {
    prop {
        ((10 - spaceMission.spacecraft.data) - spaceMission.station.receivedData) <= 1
    }
}

target Spacecraft_batteryCharge_never_under_20_Safe : Mission {
    prop {
        ! (spaceMission.spacecraft.batteryCharge <= 20)
    }
}
