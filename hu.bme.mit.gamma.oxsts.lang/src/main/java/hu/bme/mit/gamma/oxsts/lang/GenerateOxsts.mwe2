module hu.bme.mit.gamma.oxsts.lang.GenerateOxsts

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "hu.bme.mit.gamma.oxsts.lang"
				rootPath = rootPath
				runtime = {
					// Do not generate new files into src/main/java
					src = null
				}
				runtimeTest = {
					enabled = false
					// Only generate the xtext-gen files and leave the rest of the project alone
					root = null
					srcGen = '${rootPath}/language/src/testFixtures/xtext-gen'
				}
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "hu.bme.mit.gamma.oxsts.lang.Oxsts"
			fileExtensions = "oxsts"
			referencedResource = "platform:/resource/hu.bme.mit.gamma.oxsts.lang/model/oxsts.genmodel" // TODO fix project
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

            serializer = {
				generateStub = true
			}
			formatter = {
				generateStub = true
			}

			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = false
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
